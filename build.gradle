buildscript {
    ext.versions = [
            'spotless'        : '3.22.0',
            'versions'        : '0.21.0',
            'task-tree'       : '1.3.1',
            'ktlint'          : '0.29.0',
            'kotlin'          : '1.3.31',
            'coroutines'      : '1.1.1',
            'androidx'        : [
                    'appcompat'         : '1.0.2',
                    'constraint_layout' : '2.0.0-alpha3',
                    'lifecycle'         : '2.2.0-alpha01',
                    'room'              : '2.1.0-alpha03',
                    'annotation'        : '1.0.1',
                    'paging'            : '2.1.0',
                    'swiperefreshlayout': '1.0.0',
                    'browser'           : '1.0.0',
                    'emoji'             : '1.0.0'
            ],
            'mvrx'            : '1.0.0',
            'epoxy'           : '3.4.2',
            'material'        : '1.1.0-alpha06',
            'rxjava'          : '2.2.8',
            'rxandroid'       : '2.1.1',
            'rxkotlin'        : '2.3.0',
            'rxlint'          : '1.7.3',
            'rx2receiver'     : '1.0.5',
            'crashlytics'     : '2.9.9',
            'koin'            : '1.0.2',
            'kotlin_logging'  : '1.6.26',
            'slf4j'           : '1.7.26',
            'logback'         : '2.0.0',
            'retrofit'        : '2.5.0',
            'okhttp'          : '3.14.1',
            'leakcanary'      : '1.6.3',
            'threetenabp'     : '1.2.0',
            'threetenbp'      : '1.3.8',
            'glide'           : '4.9.0',
            'gson'            : '2.8.5',
            'stetho'          : '1.5.1',
    ]

    ext.buildConfig = [
            'compileSdk': 28,
            'minSdk'    : 21,
            'targetSdk' : 28,
    ]

    repositories {
        google()
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.1'
        // TODO: it's hotfic for bug https://github.com/google/dagger/issues/1245. Remove when we'll get stable solution
        classpath 'com.android.tools.build.jetifier:jetifier-processor:1.0.0-beta02'

        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.8.6'
        classpath 'io.fabric.tools:gradle:1.28.1'
    }
}

plugins {
    // We are unable to use constants due to limitations of the gradle plugin.
    // We've decided to stick with duplication for now since the other methods involve using undocumented APIs
    // https://docs.gradle.org/current/userguide/plugins.html#plugins_dsl_limitations
    // https://github.com/gradle/gradle/issues/1697
    id "com.diffplug.gradle.spotless" version "3.22.0"
    id 'com.github.ben-manes.versions' version "0.21.0"
    id "com.dorongold.task-tree" version "1.3.1"
    id "io.gitlab.arturbosch.detekt" version "1.0.0-RC14"
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

subprojects {
    apply plugin: 'com.diffplug.gradle.spotless'
    spotless {
        kotlin {
            target "**/*.kt"
            ktlint(versions.ktlint)
            endWithNewline()
        }
    }

    // solution for https://github.com/google/dagger/issues/306#issuecomment-180283287
    afterEvaluate {
        if (project.plugins.hasPlugin("kotlin-kapt")) {
            kapt {
                javacOptions {
                    option("-Xmaxerrs", 600)
                }
            }
        }
    }

    apply from: "${rootDir}/util/gradle_plugins/detekt.gradle"
}

Object propOrDef(String propertyName, Object defaultValue) {
    def propertyValue = project.properties[propertyName]
    return propertyValue != null ? propertyValue : defaultValue
}
